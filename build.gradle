plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id 'java'
}


jar {
    manifest {
        attributes(
                'Main-Class': 'com.fake_company.spark_rest_example.Application'
        )
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
        dependencies {
            integrationTestCompile sourceSets.main.output
            integrationTestCompile sourceSets.test.output

            integrationTestCompile configurations.compile
            integrationTestCompile configurations.testCompile

            integrationTestRuntime configurations.runtime
            integrationTestRuntime configurations.testRuntime
        }
    }
}

task integrationTests(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = "Runs Integration Tests"

    maxHeapSize = '2048m'

    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

    binResultsDir = file("$buildDir/integration-tests/binary/integrationTest")

    reports {
        html.destination = "$buildDir/reports/integration-test"
        junitXml.destination = "$buildDir/integration-test-results"
    }
}

check.dependsOn integrationTests

group 'spark-rest-example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.sparkjava', name: 'spark-core', version: '2.7.2'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.20'
    compile group: 'com.beust', name: 'jcommander', version: '1.72'
    compile group: 'com.zaxxer', name: 'HikariCP', version: '2.7.8'
    compile group: 'com.h2database', name: 'h2', version: '1.4.197'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.9.4'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.4'
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: '5.2.15.Final'
    compile group: 'org.hibernate', name: 'hibernate-hikaricp', version: '5.2.15.Final'
    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.15.Final'
    compile group: 'org.hibernate', name: 'hibernate-ehcache', version: '5.2.15.Final'
    compile group: 'org.hibernate', name: 'hibernate-validator', version: '6.0.9.Final'
    compile group: 'org.glassfish', name: 'javax.el', version: '3.0.0'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    testCompile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
